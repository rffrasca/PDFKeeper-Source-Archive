;*************************************************************************
;*
;* PDFKeeper -- PDF Document Storage for Small or Home Office
;* Copyright (C) 2009-2012 Robert F. Frasca
;*
;* This program is free software: you can redistribute it and/or modify
;* it under the terms of the GNU General Public License as published by
;* the Free Software Foundation, either version 3 of the License, or
;* (at your option) any later version.
;*
;* This program is distributed in the hope that it will be useful, but
;* WITHOUT ANY WARRANTY; without even the implied warranty of
;* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;* GNU General Public License for more details.
;*
;* You should have received a copy of the GNU General Public License
;* along with this program.  If not, see <http://www.gnu.org/licenses/>.
;*
;*************************************************************************

; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this ClientSide.
; Do not use the same AppId value in installers for other ClientSides.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{4F0E9E20-AB83-4AB1-9B05-D77BDED27ED4}
AppName=PDFKeeper
AppVerName=PDFKeeper 2.6.0
AppPublisher=Robert F. Frasca
AppPublisherURL=http://code.google.com/p/pdfkeeper/
AppSupportURL=http://code.google.com/p/pdfkeeper/issues/list
AppUpdatesURL=http://code.google.com/p/pdfkeeper/downloads/list
DefaultDirName={pf}\PDFKeeper
DefaultGroupName=PDFKeeper
LicenseFile=..\COPYING.txt
OutputDir=.
OutputBaseFilename=PDFKeeper-2.6.0
SetupIconFile=..\Icon\PDFKeeper.ico
Compression=lzma
SolidCompression=true
MinVersion=0,5.01.2600sp1
VersionInfoVersion=2.6.0
VersionInfoDescription=PDF Document Storage for Small or Home Office
VersionInfoTextVersion=2.6.0
VersionInfoProductName=PDFKeeper
VersionInfoProductVersion=2.6.0
AlwaysRestart=false
AppCopyright=© 2009-2012 Robert F. Frasca
AppVersion=2.6.0
AppContact=
VersionInfoCompany=Robert F. Frasca
VersionInfoCopyright=© 2009-2012 Robert F. Frasca
AppComments=PDF Document Storage for Small or Home Office
UninstallDisplayIcon={app}\Icon\PDFKeeper.ico
ChangesAssociations=false
OnlyBelowVersion=0,0
ChangesEnvironment=false
AllowUNCPath=false

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Icons]
Name: "{group}\PDFKeeper"; Filename: "{app}\PdfKeeper.Client\PdfKeeper.Client.exe"; WorkingDir: "{app}\PdfKeeper.Client"; IconFilename: "{app}\PdfKeeper.Client\PdfKeeper.Client.exe"; Comment: "PDF Document Storage Application"
Name: "{commondesktop}\PDFKeeper"; Filename: "{app}\PdfKeeper.Client\PdfKeeper.Client.exe"; WorkingDir: "{app}\PdfKeeper.Client"; IconFilename: "{app}\PdfKeeper.Client\PdfKeeper.Client.exe"; Comment: "PDF Document Storage Application"
Name: "{group}\Database Setup"; Filename: "{app}\Database Setup\DatabaseSetup.cmd"; WorkingDir: "{app}\Database Setup"
Name: "{group}\Documentation\User FAQ"; Filename: "{app}\Documentation\UserFAQ.html"; WorkingDir: "{app}\Documentation"
Name: "{group}\Documentation\Release Notes"; Filename: "{app}\Documentation\ReleaseNotes.html"; WorkingDir: "{app}\Documentation"
Name: "{group}\Documentation\Getting Started Guide"; Filename: "{app}\Documentation\GettingStartedGuide.html"; WorkingDir: "{app}\Documentation"
Name: "{group}\Documentation\User Guide"; Filename: "{app}\Documentation\UserGuide.html"; WorkingDir: "{app}\Documentation"

[Dirs]
Name: "{app}\Documentation\css"; Flags: uninsalwaysuninstall
Name: "{app}\PdfKeeper.Client"; Flags: uninsalwaysuninstall
Name: "{app}\Database Setup"; Flags: uninsalwaysuninstall
Name: "{app}\Documentation"; Flags: uninsalwaysuninstall
Name: "{app}\Icon"; Flags: uninsalwaysuninstall
Name: "{app}\Installer"; Flags: uninsalwaysuninstall

[Files]
Source: "..\THIRDPARTY.txt"; DestDir: "{app}"
Source: "..\COPYING.txt"; DestDir: "{app}"
Source: "..\Documentation\UserGuide.html"; DestDir: "{app}\Documentation"
Source: "..\Documentation\GettingStartedGuide.html"; DestDir: "{app}\Documentation"
Source: "..\Documentation\ReleaseNotes.html"; DestDir: "{app}\Documentation"
Source: "..\Documentation\UserFAQ.html"; DestDir: "{app}\Documentation"
Source: "..\Documentation\readyset-license.html"; DestDir: "{app}\Documentation"
Source: "..\Documentation\css\readyset.css"; DestDir: "{app}\Documentation\css"
Source: "..\Documentation\css\inst.css"; DestDir: "{app}\Documentation\css"
Source: "..\Documentation\css\nav.css"; DestDir: "{app}\Documentation\css"
Source: "..\Documentation\css\print.css"; DestDir: "{app}\Documentation\css"
Source: "..\Icon\PDFKeeper.ico"; DestDir: "{app}\Icon"
Source: "..\PDFKeeper.Client\bin\Release\PdfKeeper.Client.exe"; DestDir: "{app}\PdfKeeper.Client"
Source: "..\Dependencies\SumatraPDF.exe"; DestDir: "{app}\PdfKeeper.Client"
Source: "..\Dependencies\sumatrapdfrestrict.ini"; DestDir: "{app}\PdfKeeper.Client"
Source: "..\Dependencies\itextsharp.dll"; DestDir: "{app}\PdfKeeper.Client"
Source: "..\Dependencies\Nini.dll"; DestDir: "{app}\PdfKeeper.Client"
Source: "..\Dependencies\ckill.exe"; DestDir: "{app}\Installer"
Source: "..\Database Setup\DatabaseSetup.cmd"; DestDir: "{app}\Database Setup"
Source: "..\Database Setup\DatabaseSetup.sql"; DestDir: "{app}\Database Setup"

[Run]
Filename: "{app}\Documentation\GettingStartedGuide.html"; WorkingDir: "{app}\Documentation"; Flags: shellexec

[Registry]

[UninstallRun]
Filename: "{app}\Installer\ckill.exe"; Parameters: "PdfKeeper.Client.exe"; Flags: waituntilterminated runhidden

[Code]
function InitializeSetup(): Boolean;
var
	uninstaller: String;
	resultCode: Integer;
begin
	Result := true;
  
  // An existing installation of PDFKeeper must be uninstalled.
  if RegKeyExists(HKEY_LOCAL_MACHINE,
	      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{4F0E9E20-AB83-4AB1-9B05-D77BDED27ED4}_is1') then
	begin
    if MsgBox('Setup has detected an existing installation of PDFKeeper that must be removed. Do you wish to continue?',
					     mbConfirmation, MB_YESNO) = IDNO then
      Result := False
		else
		  RegQueryStringValue(HKEY_LOCAL_MACHINE,
			      					   'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{4F0E9E20-AB83-4AB1-9B05-D77BDED27ED4}_is1',
						      		   'UninstallString', uninstaller);
			ShellExec('open', uninstaller,
			  			  '/SILENT /NORESTART /SUPPRESSMSGBOXES',
				  		  '', SW_HIDE, ewWaitUntilTerminated, resultCode);
			if not (ResultCode = 0) then
			  Result := false
			else
				Result := true;
  end;
end;
