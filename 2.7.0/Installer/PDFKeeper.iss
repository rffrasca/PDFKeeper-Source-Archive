;******************************************************************************
;*
;* PDFKeeper -- PDF Document Storage for Small or Home Office
;* Copyright (C) 2009-2012 Robert F. Frasca
;*
;* This program is free software: you can redistribute it and/or modify it
;* under the terms of the GNU General Public License as published by the Free
;* Software Foundation, either version 3 of the License, or (at your option)
;* any later version.
;*
;* This program is distributed in the hope that it will be useful, but WITHOUT
;* ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
;* FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
;* more details.
;*
;* You should have received a copy of the GNU General Public License along
;* with this program.  If not, see <http://www.gnu.org/licenses/>.
;*
;******************************************************************************

; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this ClientSide.
; Do not use the same AppId value in installers for other ClientSides.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{4F0E9E20-AB83-4AB1-9B05-D77BDED27ED4}
AppName=PDFKeeper
AppVersion=2.7.0
AppPublisher=Robert F. Frasca
AppPublisherURL=http://code.google.com/p/pdfkeeper/
AppSupportURL=http://code.google.com/p/pdfkeeper/issues/list
AppUpdatesURL=http://code.google.com/p/pdfkeeper/downloads/list
DefaultDirName={pf}\PDFKeeper
DefaultGroupName=PDFKeeper
LicenseFile=..\COPYING.txt
OutputDir=.
OutputBaseFilename=PDFKeeper-2.7.0
SetupIconFile=..\Icon\PDFKeeper.ico
Compression=lzma
SolidCompression=true
MinVersion=0,5.01.2600sp1
VersionInfoVersion=2.7.0
VersionInfoDescription=PDF Document Storage for Small or Home Office
VersionInfoTextVersion=2.7.0
VersionInfoProductName=PDFKeeper
VersionInfoProductVersion=2.7.0
AlwaysRestart=false
AppCopyright=© 2009-2012 Robert F. Frasca
AppContact=mailto:rffrasca@gmail.com
VersionInfoCompany=Robert F. Frasca
VersionInfoCopyright=© 2009-2012 Robert F. Frasca
AppComments=PDF Document Storage for Small or Home Office
UninstallDisplayIcon={app}\Bin\PdfKeeper.Client.exe
ChangesAssociations=false
OnlyBelowVersion=0,0
ChangesEnvironment=false
AllowUNCPath=false

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Icons]
Name: "{group}\PDFKeeper"; Filename: "{app}\Bin\PdfKeeper.Client.exe"; WorkingDir: "{app}\Bin"; IconFilename: "{app}\Bin\PdfKeeper.Client.exe"; Comment: "PDF Document Storage Application"
Name: "{commondesktop}\PDFKeeper"; Filename: "{app}\Bin\PdfKeeper.Client.exe"; WorkingDir: "{app}\Bin"; IconFilename: "{app}\Bin\PdfKeeper.Client.exe"; Comment: "PDF Document Storage Application"
Name: "{group}\Database Setup"; Filename: "{app}\Setup\DatabaseSetup.cmd"; WorkingDir: "{app}\Setup"
Name: "{group}\Documentation\User FAQ"; Filename: "{app}\Help\UserFAQ.html"; WorkingDir: "{app}\Help"
Name: "{group}\Documentation\Release Notes"; Filename: "{app}\Help\ReleaseNotes.html"; WorkingDir: "{app}\Help"
Name: "{group}\Documentation\Getting Started Guide"; Filename: "{app}\Help\GettingStartedGuide.html"; WorkingDir: "{app}\Help"
Name: "{group}\Documentation\User Guide"; Filename: "{app}\Help\UserGuide.html"; WorkingDir: "{app}\Help"

[Dirs]
Name: "{app}\Bin"; Flags: uninsalwaysuninstall
Name: "{app}\Help"; Flags: uninsalwaysuninstall
Name: "{app}\Help\css"; Flags: uninsalwaysuninstall
Name: "{app}\License"; Flags: uninsalwaysuninstall
Name: "{app}\Setup"; Flags: uninsalwaysuninstall

[Files]
Source: "..\COPYING.txt"; DestDir: "{app}\License"
Source: "..\3rdParty\THIRDPARTYNOTICES.txt"; DestDir: "{app}\License"
Source: "..\3rdParty\readyset-license.html"; DestDir: "{app}\License"
Source: "..\3rdParty\CKill\gpl.txt"; DestDir: "{app}\License"; DestName: "CKill.txt"
Source: "..\Documentation\UserGuide.html"; DestDir: "{app}\Help"
Source: "..\Documentation\GettingStartedGuide.html"; DestDir: "{app}\Help"
Source: "..\Documentation\ReleaseNotes.html"; DestDir: "{app}\Help"
Source: "..\Documentation\UserFAQ.html"; DestDir: "{app}\Help"
Source: "..\Documentation\css\readyset.css"; DestDir: "{app}\Help\css"
Source: "..\Documentation\css\inst.css"; DestDir: "{app}\Help\css"
Source: "..\Documentation\css\nav.css"; DestDir: "{app}\Help\css"
Source: "..\Documentation\css\print.css"; DestDir: "{app}\Help\css"
Source: "..\PDFKeeper.Client\bin\Release\PdfKeeper.Client.exe"; DestDir: "{app}\Bin"
Source: "..\PDFKeeper.Client\bin\Release\SumatraPDF.exe"; DestDir: "{app}\Bin"
Source: "..\PDFKeeper.Client\bin\Release\sumatrapdfrestrict.ini"; DestDir: "{app}\Bin"
Source: "..\PDFKeeper.Client\bin\Release\itextsharp.dll"; DestDir: "{app}\Bin"
Source: "..\PDFKeeper.Client\bin\Release\Nini.dll"; DestDir: "{app}\Bin"
Source: "..\Database Setup\DatabaseSetup.cmd"; DestDir: "{app}\Setup"
Source: "..\Database Setup\DatabaseSetup.sql"; DestDir: "{app}\Setup"
Source: "..\3rdParty\CKill\ckill.exe"; DestDir: "{app}\Setup"

[Run]
Filename: "{app}\Help\GettingStartedGuide.html"; WorkingDir: "{app}\Help"; Flags: shellexec

[Registry]

[UninstallRun]
Filename: "{app}\Setup\ckill.exe"; Parameters: "PdfKeeper.Client.exe"; Flags: waituntilterminated runhidden

[UninstallDelete]

[Code]
procedure CurStepChanged(CurStep: TSetupStep);
var
  uninstaller: String;
	resultCode: Integer;
  handle: HWND;
begin

  // Uninstall existing version.
  if CurStep = ssInstall then
  begin
    if RegKeyExists(HKEY_LOCAL_MACHINE,
        'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{4F0E9E20-AB83-4AB1-9B05-D77BDED27ED4}_is1') then
    begin
      RegQueryStringValue(HKEY_LOCAL_MACHINE,
			  'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{4F0E9E20-AB83-4AB1-9B05-D77BDED27ED4}_is1',
				'UninstallString', uninstaller);
      if not ShellExec('', uninstaller, '/VERYSILENT /NORESTART /SUPPRESSMSGBOXES',
                       '', SW_HIDE, ewWaitUntilTerminated, resultCode) then
      begin
        Exit;
      end;
      repeat
        Sleep(1000);
        handle := FindWindowByWindowName('PDFKeeper Uninstall')
      until handle = 0;
    end;
  end;
end;
